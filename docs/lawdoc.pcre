(?x)  # Extended mode
(?s)  # Dotall mode
#
# This is a PCRE2 RegEx version of the JSON schmema in lawdoc.schema. The main
# advantage of this version of the schema is, that it can also check if the
# elements in a LawDoc Part are in the right order, whereas the description in
# lawdoc.schema only check for valid parent-child pairings.
#
# For simplicity this regex assumes well-formated JSON input and doesn't check
# for things like valid escape sequences in strings and so forth.
#
# (.. still under constructions)

# $1 = capture the initial matching (valid) portion of the file
(\A\s*\{
  \s*"\$schema"\s*:\s*"https://raw\.githubusercontent\.com/clairexen/LawAT/refs/heads/main/docs/lawdoc\.json"\s*,
  \s*"document"\s*: (?&LawDoc)
# \s*\}\s*\z # do not match the closing } so we match the valid
)            # parts until the part containing the invalid code

# $2 = capture the first non-valid entity (Part or Meta), if we can
( (?&Any)? )

# $3 = capture the rest of the subject string
( .+? ) \z

(?(DEFINE)

(?<LawDoc>
  \s*\[
    \s*"LawDoc (?&StrTail)
    (?: (?&Meta) | (?&Part) )+
# \s*\]\s*,? # do not match the closing ] so we match the valid
)            # parts until the part containing the invalid code

(?<Meta>
  \s*\[
    \s*"Meta (?&StrTail) (?&Str)*
  \s*\]\s*,?
)

(?<Part>
  \s*\[
    \s*"Part (?&StrTail) (?&PartContent)
  \s*\]\s*,?
)

(?<PartContent>
  (?&Head)*
  (?&Title)?
  (?&Content)
  (?: (?&SubHdr)* (?&Content) )*
)

(?<Content>
  (?&Text) | (?&List) | (?&Table) | (?&Media)
  (?: (?&Break)? (?: (?&Text) | (?&List) | (?&Table) | (?&Media) ) )*
)

(?<Head>
  \s*\[
    \s*"Head(?&OptStrTail) (?&TextContent)
  \s*\]\s*,?
)

(?<Title>
  \s*\[
    \s*"Title(?&OptStrTail) (?&TextContent)
  \s*\]\s*,?
)

(?<SubHdr>
  \s*\[
    \s*"SubHdr(?&OptStrTail) (?&TextContent)
  \s*\]\s*,?
)

(?<Break>
  \s*\[
    \s*"Break"\s*
  \s*\]\s*,?
)

(?<Text>
  \s*\[
    \s*"Text(?&OptStrTail) (?&TextContent)
  \s*\]\s*,?
)

(?<Rem>
  \s*\[
    \s*"Rem"\s*,? (?&Str)+
  \s*\]\s*,?
)

(?<List>
  \s*\[
    \s*"List(?&OptStrTail) (?: (?&Item) | (?&Rem) )+
  \s*\]\s*,?
)

(?<Item>
  \s*\[
    \s*"Item(?&OptStrTail) (?&Content)+
  \s*\]\s*,?
)

(?<Table>
  FIXME
)

(?<Media>
  FIXME
)

(?<TextContent> (?: (?&Str) | (?&Rem) )+ )

(?<OptTextContent> (?: (?&Str) | (?&Rem) )* )

(?<Str> \s*"(?: \\. | [^\\"])*"\s*,? )

(?<StrTail> (?: \\. | [^\\"])*"\s*,? )

(?<OptStrTail> (?: \s+(?: \\. | [^\\"])*|)"\s*,? )

(?<Any> \s*\[(?&Any)*\s*]\s*,? | (?&Str) )

) # end DEFINE
